plugins {
    id 'maven-publish'
    id 'signing'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addStringOption("Xdoclint:none", "-quiet")
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                url = "https://github.com/arconia-io/arconia"
                inceptionYear = '2024'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = "ThomasVitale"
                        name = "Thomas Vitale"
                        url = "https://thomasvitale.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/arconia-io/arconia.git"
                    developerConnection = "scm:git:ssh://github.com/arconia-io/arconia.git"
                    url = "https://github.com/arconia-io/arconia"
                }
            }
        }
    }

    repositories {
        maven {
            name = "SonatypeSnapshot"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

            credentials {
                username = findProperty("mavenCentralUsername") ?: System.getenv("MAVEN_CENTRAL_USERNAME")
                password = findProperty("mavenCentralToken") ?: System.getenv("MAVEN_CENTRAL_TOKEN")
            }
        }
        maven {
            name = "SonatypeStaging"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username = findProperty("mavenCentralUsername") ?: System.getenv("MAVEN_CENTRAL_USERNAME")
                password = findProperty("mavenCentralToken") ?: System.getenv("MAVEN_CENTRAL_TOKEN")
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("release") }
    def signingSecretKey = findProperty('signingSecretKey') ?: System.getenv("SIGNING_SECRET_KEY")
    def signingPassphrase = findProperty('signingPassphrase') ?: System.getenv("SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingSecretKey, signingPassphrase)
    sign publishing.publications.mavenJava
}

tasks.register('snapshot') {
    onlyIf { !isReleaseVersion }
    finalizedBy 'publishAllPublicationsToSonatypeSnapshotRepository'
}

tasks.register('release') {
    onlyIf { isReleaseVersion }
    finalizedBy 'publishAllPublicationsToSonatypeStagingRepository'
}
