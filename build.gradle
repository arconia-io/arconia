plugins {
    id 'org.jreleaser' version '1.15.0'
}

// JReleaser requires this task in the root project.
tasks.register("clean", Delete.class) {
    group 'build'
    delete 'build'
}

jreleaser {
    project {
        name = "arconia"
        description = "Java framework to build SaaS, multitenant applications"
        authors = ["Thomas Vitale"]
        tags = ['spring-boot', 'multitenancy', 'saas']
        maintainers = ['ThomasVitale']
        license = 'Apache-2.0'
        inceptionYear = "2024"
        copyright = '#{project.inceptionYear} #{project.authors}'
    }

    checksum {
        algorithm('SHA-256')
        algorithm('SHA-512')
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    applyMavenCentralRules = true
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }

    release {
        github {
            repoOwner = 'arconia-io'
            releaseName = '{{tagName}}'

            changelog {
                formatted = 'ALWAYS'
                preset = 'conventional-commits'
                extraProperties.put('categorizeScopes', true)
            }
        }
    }
}
